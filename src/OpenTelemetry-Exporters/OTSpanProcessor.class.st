"
I am responsible for processing and handling `OTSpan`s generated by an application.
I receive spans as they are created and am responsible for applying various processing steps to them.
These steps may include filtering, sampling, enriching, and forwarding to exporters.
Spans are processed in real time, providing immediate feedback and insight into application behavior.

By implementing custom SpanProcessor classes, developers can tailor span processing behavior to meet specific use cases and requirements.
"
Class {
	#name : 'OTSpanProcessor',
	#superclass : 'Object',
	#classVars : [
		'Current'
	],
	#category : 'OpenTelemetry-Exporters',
	#package : 'OpenTelemetry-Exporters'
}

{ #category : 'instance creation' }
OTSpanProcessor class >> beCurrent [

	<script>
	self isAbstract
		ifTrue: [ Error signal: self name , ' is abstract' ]
		ifFalse: [
			^ self == Current class
				  ifTrue: [ Current ]
				  ifFalse: [
					  Current flush.
					  Current := self new ] ]
]

{ #category : 'accessing' }
OTSpanProcessor class >> current [

	^ Current ifNil: [ Current := self readConfiguration ]
]

{ #category : 'accessing' }
OTSpanProcessor class >> current: aSpanProcessor [

	Current := aSpanProcessor
]

{ #category : 'processing' }
OTSpanProcessor class >> flush [

	<script>
	Current ifNotNil: [ Current flush ]
]

{ #category : 'testing' }
OTSpanProcessor class >> isAbstract [

	^ self == OTSpanProcessor
]

{ #category : 'instance creation' }
OTSpanProcessor class >> readConfiguration [
	"Use defaults for now, later should use properties or env vars as explained in https://opentelemetry.io/docs/instrumentation/java/automatic/agent-config/#configuring-the-agent"

	^ OTBatchSpanProcessor new
]

{ #category : 'class initialization' }
OTSpanProcessor class >> reset [

	self flush.
	Current := nil
]

{ #category : 'accessing' }
OTSpanProcessor >> beCurrent [

	^ Current := self
]

{ #category : 'processing' }
OTSpanProcessor >> flush [
	"Redefined by subclasses to terminate current span processing and forward to exporter."
]

{ #category : 'processing' }
OTSpanProcessor >> process: span [
	"Export the span and return true if it was accepted"

	self subclassResponsibility
]
